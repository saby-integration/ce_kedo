
&После("ОбработчикКоманды")
Процедура Saby_ОбработчикКоманды(ФормаОтчета, Команда, Результат)
	Если 	ФормаОтчета.ИмяФормы = "Отчет.АнализНачисленийИУдержаний.Форма"
				и
			Найти(ФормаОтчета.ВариантыПанелиКлючТекущегоВарианта, "РасчетныйЛисток") > 0
				и
			(Команда.Имя = "Saby_ОтправитьРасчетныйЛистокВСбис" или
			Команда.Имя = "Saby_ОтправитьПрочийКадровыйРасчетныйЛистокВСбис" или
			Команда.Имя = "Saby_ОтправитьСокращенныйРасчетныйЛистокВСбис") Тогда
			
			ПараметрыВызова = Новый Структура("НачалоПериода,КонецПериода,ФизическиеЛица,Организация,ЕстьФильтрПоСотрудникам,ЕстьФильтрПоОрганизации");
			Для Каждого ЭлементОтбора Из ФормаОтчета.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
				//Взаимоисключающиеся параметры.
				Если	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
					ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.Организации") Тогда
					//Элемент - Организации
					ПараметрыВызова.Организация				= ЭлементОтбора.ПравоеЗначение;
					ПараметрыВызова.ЕстьФильтрПоОрганизации	= ЭлементОтбора.Использование;
				ИначеЕсли	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
					ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					//Элемент - ФизическиеЛица
					ПараметрыВызова.ФизическиеЛица			= ЭлементОтбора.ПравоеЗначение;
					ПараметрыВызова.ЕстьФильтрПоСотрудникам	= ЭлементОтбора.Использование;
				ИначеЕсли	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
					ЭлементОтбора.ПравоеЗначение <> Неопределено И
					ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Организации") Тогда
					//Список - Организации
					ПараметрыВызова.Организация				= ЭлементОтбора.ПравоеЗначение;
					ПараметрыВызова.ЕстьФильтрПоОрганизации	= ЭлементОтбора.Использование;
				ИначеЕсли	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
					ЭлементОтбора.ПравоеЗначение <> Неопределено И
					ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица") Тогда
					//Список - ФизическиеЛица
					ПараметрыВызова.ФизическиеЛица			= ЭлементОтбора.ПравоеЗначение;
					ПараметрыВызова.ЕстьФильтрПоСотрудникам	= ЭлементОтбора.Использование;
				ИначеЕсли	ТипЗнч(ЭлементОтбора) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ТипЗнч(ЭлементОтбора.Значение) = Тип("СтандартныйПериод") Тогда
					ПараметрыВызова.НачалоПериода			= ЭлементОтбора.Значение.ДатаНачала;
					ПараметрыВызова.КонецПериода			= ЭлементОтбора.Значение.ДатаОкончания;
				КонецЕсли;
			КонецЦикла;
		Если Команда.Имя = "Saby_ОтправитьРасчетныйЛистокВСбис" Тогда	
			Saby_КомандыОбменаДляФормыКлиент.ОтправитьРасчетныйЛистокВСБИС(ПараметрыВызова);
		ИначеЕсли Команда.Имя = "Saby_ОтправитьСокращенныйРасчетныйЛистокВСбис" Тогда	
			Saby_КомандыОбменаДляФормыКлиент.ОтправитьСокращенныйРасчетныйЛистокВСБИС(ПараметрыВызова);
		ИначеЕсли Команда.Имя = "Saby_ОтправитьПрочийКадровыйРасчетныйЛистокВСбис" Тогда
			Saby_КомандыОбменаДляФормыКлиент.ОтправитьПрочийКадровыйРасчетныйЛистокВСБИС(ПараметрыВызова);
		КонецЕсли;	
		Результат = Истина;
	КонецЕсли;
КонецПроцедуры
