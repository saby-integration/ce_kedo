
Функция НайтиЭлементФормыПоИмени(ДочерниеЭлементы, ИмяЭлемента)
	СтрокаПоиска		= Неопределено;
	Для Каждого ЭлементДочерний Из  ДочерниеЭлементы Цикл
		Если Найти(ЭлементДочерний.Имя, ИмяЭлемента) > 0 Тогда
			СтрокаПоиска	= ЭлементДочерний;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат СтрокаПоиска;
КонецФункции

&После("ВывестиКоманды")
Процедура Integration_ВывестиКоманды(Форма, Команды, ПараметрыРазмещения)
	// В бухгалтерии нужно немного подвинуть группу команд в лево.
	// иначе ее не видно на панели задач
	Если Найти(Метаданные.Имя, "Бухгалтерия") = 0 Тогда
		Возврат;
	КонецЕсли;
	Если ПараметрыРазмещения.КоманднаяПанель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РодительЭлемента	= Форма.Элементы.Найти(ПараметрыРазмещения.КоманднаяПанель.Имя);
	ДочерниеЭлементы	= РодительЭлемента.ПодчиненныеЭлементы;
	НазваниеПродукта 	= ЛокализацияНазваниеПродукта();
	Подгруппа			= ДочерниеЭлементы.Найти(НазваниеПродукта + "_Integration");
	
	СтрокаПоиска = НайтиЭлементФормыПоИмени(ДочерниеЭлементы, "СтрокаПоиска");
	Если
		РодительЭлемента <> Неопределено
		И СтрокаПоиска <> Неопределено
		И Подгруппа <> Неопределено
									Тогда
		Форма.Элементы.Переместить(Подгруппа, РодительЭлемента, СтрокаПоиска);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция УсловияДобавленияКолонокВыполнены(МодульВстраиваниеВСтандартныеФормы, МодульCore, Форма)
	Если 
		Не МодульВстраиваниеВСтандартныеФормы.ЭтоФормаСписка(Форма)
		ИЛИ МодульВстраиваниеВСтандартныеФормы.ЭтоФормаИсключения(Форма)
		// Проверка прав
		ИЛИ НЕ МодульCore.ТекущемуПользователюМожноРаботатьСРасширением()
		ИЛИ НЕ МодульCore.ВключеноИспользованиеПодсистемыКЕДО()
		Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&После("ПриСозданииНаСервере")
Процедура Integration_ПриСозданииНаСервере(Форма, Знач ПараметрыРазмещения = Неопределено) Экспорт
	// Добавление колонок
	МодульВстраиваниеВСтандартныеФормы = МодульВстраиваниеВСтандартныеФормы(); 
	МодульCore = МодульCore();
	Если НЕ УсловияДобавленияКолонокВыполнены(МодульВстраиваниеВСтандартныеФормы, МодульCore, Форма) Тогда
		Возврат;
	КонецЕсли;
	МодульВстраиваниеВСтандартныеФормы.ДобавитьКолонкиВДинамическийСписок(Форма, ПараметрыРазмещения);
КонецПроцедуры
