
// BSP.3001007294

#Область include_core_base_locale_ЛокализацияНазваниеПродукта
#КонецОбласти

&Вместо("ВидыКоманд")
Функция Saby_ВидыКоманд()
	
	Результат = ПродолжитьВызов();
	Если Saby_Core.ТекущемуПользователюМожноРаботатьСоСБИС() ИЛИ
		Saby_Core.ВключеноИспользованиеПодсистемыКЕДО() Тогда
		// СБИС
		Вид				= Результат.Добавить();
		Вид.Заголовок	= ЛокализацияНазваниеПродукта();
		Вид.Имя			= "SABY_КОМАНДЫ";
		Вид.ИмяПодменю	= "Saby_СБИС";
		Вид.Порядок		= 20;
		Вид.Картинка    = Новый Картинка;
		Вид.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&После("ВывестиКоманды")
Процедура Saby_ВывестиКоманды(Форма, Команды, ПараметрыРазмещения)
	//В бухгалтерии нужно немного подвинуть группу команд в лево. иначе ее не видно на панели задач
	Если Найти(Метаданные.Имя, "Бухгалтерия") = 0 Тогда Возврат; КонецЕсли;
	Если ПараметрыРазмещения.КоманднаяПанель = Неопределено Тогда Возврат; КонецЕсли;
	РодительЭлемента	= Форма.Элементы.Найти(ПараметрыРазмещения.КоманднаяПанель.Имя);
	ДочерниеЭлементы	= РодительЭлемента.ПодчиненныеЭлементы;
	ПодгруппаСБИС		= ДочерниеЭлементы.Найти("Saby_СБИС");
	СтрокаПоиска		= Неопределено;
	Для Каждого ЭлементДочерний Из  ДочерниеЭлементы Цикл
		Если Найти(ЭлементДочерний.Имя, "СтрокаПоиска" ) > 0 Тогда
			СтрокаПоиска	= ЭлементДочерний;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если
		РодительЭлемента <> Неопределено И
		СтрокаПоиска <> Неопределено И
		ПодгруппаСБИС <> Неопределено
											Тогда
		Форма.Элементы.Переместить(ПодгруппаСБИС, РодительЭлемента, СтрокаПоиска);
	КонецЕсли;
КонецПроцедуры

&После("ПриСозданииНаСервере")
Процедура Saby_ПриСозданииНаСервере(Форма, ПараметрыРазмещения = Неопределено) Экспорт
	//Добавление колонок

	Если 
		Не Saby_ВстраиваниеВСтандартныеФормы.ЭтоФормаСписка(Форма) ИЛИ
		Saby_ВстраиваниеВСтандартныеФормы.ЭтоФормаИсключения(Форма) ИЛИ
		//Проверка прав
		НЕ Saby_Core.ТекущемуПользователюМожноРаботатьСоСБИС() ИЛИ
		НЕ Saby_Core.ВключеноИспользованиеПодсистемыКЕДО()
		Тогда
		Возврат; 
	КонецЕсли;
	Saby_ВстраиваниеВСтандартныеФормы.ДобавитьКолонкиВДинамическийСписок(Форма);
	
	//Если вдруг нужно раскрасить форму, колонку, нам сюда
	//Saby_КолонкиСостояния.УстановитьУсловноеОформление(Форма, НаименованиеРеквизитаСписок, ГруппаНастроек, ПараметрыКритерияОтбора, ГруппаПараметровКритерияОтбора, УспешноеВыполнение);
	
	//Придет время и нас попросят добавить на формы  отбора поле
	//Saby_КолонкиСостояния.УстановитьФильтры(Форма, НаименованиеРеквизитаСписок, ГруппаНастроек, ПараметрыКритерияОтбора, ГруппаПараметровКритерияОтбора, УспешноеВыполнение);
КонецПроцедуры
