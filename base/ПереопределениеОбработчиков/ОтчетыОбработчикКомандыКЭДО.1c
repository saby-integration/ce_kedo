Процедура Saby_ЗаполнитьЗначениПараметраОтбора(ПараметрыВызова, ЭлементОтбора, ОтбораЗначение, ОтбораИспользование)
	ПараметрыВызова.Вставить(ОтбораЗначение, ЭлементОтбора.ПравоеЗначение);
	ПараметрыВызова.Вставить(ОтбораИспользование, ЭлементОтбора.Использование);
КонецПроцедуры

Процедура Saby_ЗаполнитьЗначенияПараметровОтбора(ПользовательскиеНастройки, ПараметрыВызова)
		Для Каждого ЭлементОтбора Из ПользовательскиеНастройки Цикл
			// Взаимоисключающиеся параметры.
			Если	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
				И ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.Организации") Тогда
				// Организации
				Saby_ЗаполнитьЗначениПараметраОтбора(ПараметрыВызова, ЭлементОтбора, "Организация", "ЕстьФильтрПоОрганизации");
			ИначеЕсли	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
				И ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				// ФизическиеЛица
				Saby_ЗаполнитьЗначениПараметраОтбора(ПараметрыВызова, ЭлементОтбора, "ФизическиеЛица", "ЕстьФильтрПоСотрудникам");
			ИначеЕсли	ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
				 И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке 
					Тогда
					Если ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Организации") Тогда
						// Список - Организации
						Saby_ЗаполнитьЗначениПараметраОтбора(ПараметрыВызова, ЭлементОтбора, "Организация", "ЕстьФильтрПоОрганизации");
					Иначе
						Если ЭлементОтбора.ПравоеЗначение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица") Тогда
							// Список - ФизическиеЛица
							Saby_ЗаполнитьЗначениПараметраОтбора(ПараметрыВызова, ЭлементОтбора, "ФизическиеЛица", "ЕстьФильтрПоСотрудникам");
						КонецЕсли;	
					КонецЕсли;	
			Иначе
				Если	ТипЗнч(ЭлементОтбора) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")
					И ТипЗнч(ЭлементОтбора.Значение) = Тип("СтандартныйПериод")
						Тогда
					ПараметрыВызова.НачалоПериода			= ЭлементОтбора.Значение.ДатаНачала;
					ПараметрыВызова.КонецПериода			= ЭлементОтбора.Значение.ДатаОкончания;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
КонецПроцедуры

Процедура Saby_ОбработчикКоманды_КЭДО(ФормаОтчета, Команда, Результат)
	Если 	ФормаОтчета.ИмяФормы = "Отчет.АнализНачисленийИУдержаний.Форма" Тогда
			
		ПараметрыВызова = Новый Структура("НачалоПериода,КонецПериода,ФизическиеЛица,Организация,ЕстьФильтрПоСотрудникам,ЕстьФильтрПоОрганизации");
        Saby_ЗаполнитьЗначенияПараметровОтбора(ФормаОтчета.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы, ПараметрыВызова);
		
		Если Команда.Имя = "Saby_КЭДО_ОтправитьРасчетныйЛистокВСбис" Тогда	
			Saby_КомандыОбменаДляФормыКлиент.ОтправитьРасчетныйЛистокВСБИС(ПараметрыВызова);
		ИначеЕсли Команда.Имя = "Saby_КЭДО_ОтправитьСокращенныйРасчетныйЛистокВСбис" Тогда	
			Saby_КомандыОбменаДляФормыКлиент.ОтправитьСокращенныйРасчетныйЛистокВСБИС(ПараметрыВызова);
		ИначеЕсли Команда.Имя = "Saby_КЭДО_ОтправитьПрочийКадровыйРасчетныйЛистокВСбис" Тогда
			Saby_КомандыОбменаДляФормыКлиент.ОтправитьПрочийКадровыйРасчетныйЛистокВСБИС(ПараметрыВызова);
		КонецЕсли;	
		Результат = Истина;
	КонецЕсли;
КонецПроцедуры

