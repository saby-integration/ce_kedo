
Процедура Saby_ЗаписьДокументаСоСреднимЗаработкомПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
 	Если НЕ Источник.ДополнительныеСвойства.Свойство("ЗаписьОбновлениеДокумента") Тогда
		// Код только для загружаемых расширением документов
		Возврат;
	КонецЕсли;
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ОбщегоНазначения.ЕстьРеквизитОбъекта("СреднийЗаработок",  Источник.Метаданные()) Тогда
		Возврат;
	КонецЕсли;
 	Если Источник.СреднийЗаработок <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка	
		РассчитатьСреднийЗаработокИНачисления(Источник);
	Исключение
		ИнфоОбОшибке	= ИнформацияОбОшибке();
		СтруктураОшибки	= Saby_Core.ExtExceptionAnalyse(ИнфоОбОшибке);
		Saby_Core.ExtExceptionToJournal(СтруктураОшибки);
	КонецПопытки;
КонецПроцедуры

Процедура РассчитатьСреднийЗаработокИНачисления(Объект)
	
	Объект.ОтработанноеВремяДляСреднегоОбщий.Очистить();
	Объект.СреднийЗаработокОбщий.Очистить();
	
	НачалоПериода = ДобавитьМесяц(НачалоМесяца(Объект.ДатаНачалаСобытия), -12);
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
		Истина,
		Объект.Сотрудник,
		"ДатаПриема, Подразделение",
		Объект.ДатаНачалаОсновногоОтпуска);
	ДатаПриемаНаРаботу = КадровыеДанные[0].ДатаПриема;
	
	НачалоПериода = Макс(НачалоПериода, НачалоМесяца(ДатаПриемаНаРаботу));
	
	ОкончаниеПериода = Макс(НачалоМесяца(Объект.ДатаНачалаСобытия) - 1, НачалоПериода);
	
	ДанныеДляРасчета = РасчетЗарплатыДляНебольшихОрганизаций.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
		Объект.Сотрудник,
		Объект.Организация,
		НачалоПериода,
		ОкончаниеПериода);
	
	Для каждого ДанныеОВремени Из ДанныеДляРасчета.ДанныеОВремени Цикл
		СтрокаДанныхОВремени = Объект.ОтработанноеВремяДляСреднегоОбщий.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныхОВремени, ДанныеОВремени);
		СтрокаДанныхОВремени.Сотрудник = Объект.Сотрудник;
	КонецЦикла;
	
	Для каждого ДанныеОНачислениях Из ДанныеДляРасчета.ДанныеОНачислениях Цикл
		СтрокаДанныеОНачислениях = Объект.СреднийЗаработокОбщий.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДанныеОНачислениях, ДанныеОНачислениях);
		СтрокаДанныеОНачислениях.Сотрудник = Объект.Сотрудник;
	КонецЦикла;
	
	ТаблицыПоСотруднику = Новый Структура;
	ТаблицыПоСотруднику.Вставить("ДанныеОНачислениях", Объект.СреднийЗаработокОбщий);
	ТаблицыПоСотруднику.Вставить("ДанныеОВремени", Объект.ОтработанноеВремяДляСреднегоОбщий);
	
	ПараметрыПолученияДанныхСреднего = РасчетЗарплатыДляНебольшихОрганизаций.ПараметрыПолученияДанныхСреднегоОбщего();
	ПараметрыПолученияДанныхСреднего.Вставить("ТаблицыПоСотруднику", 	ТаблицыПоСотруднику); 
	ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаПериода",  	НачалоПериода); 
	ПараметрыПолученияДанныхСреднего.Вставить("ДатаОкончанияПериода",	ОкончаниеПериода); 
	ПараметрыПолученияДанныхСреднего.Вставить("ДатаНачалаСобытия", 		Объект.ДатаНачалаСобытия);
	
	ДанныеРасчетаСреднегоЗаработка = РасчетЗарплатыДляНебольшихОрганизаций.ДанныеРасчетаСреднегоЗаработкаОбщего(
		ПараметрыПолученияДанныхСреднего);
	Объект.СреднийЗаработок = ДанныеРасчетаСреднегоЗаработка.Итоги.СреднедневнойЗаработок;
	
	МесяцСобытия = НачалоМесяца(Объект.ДатаНачалаСобытия);
КонецПроцедуры
